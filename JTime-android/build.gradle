buildscript {
    ext.kotlin_version = '1.0.3'
    ext.dokka_version = '0.9.9'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokka_version}"
        classpath 'com.google.gms:google-services:2.0.0-alpha5'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 8
        versionName "1.3.0"
        applicationId "com.ismail_s.jtime.android"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/sharedTest/kotlin'
        androidTest.java.srcDirs += 'src/sharedTest/kotlin'
    }

    signingConfigs {
        release {
            storeFile file("android.jks")
            keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS")
            storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD")
            keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            FileCollection proGuardFileCollection = files {file('./proguard-config').listFiles()}
            proguardFiles(proGuardFileCollection)
        }
    }
}

play {
    track = 'beta'
    jsonFile = file('google-play-publisher-keys.json')
}

dependencies {

    compile('com.mikepenz:materialdrawer:5.1.6@aar') {
        transitive = true
    }

    // Espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'

    // Robolectric
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3' // fixes issue on linux/mac
    testCompile 'org.robolectric:robolectric:3.1.2'
    testCompile 'org.robolectric:shadows-support-v4:3.1.2'
    //This line is needed to avoid dep conflicts
    compile 'com.google.guava:guava:19.0'

    //Android support library
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:support-v13:23.1.1'
    androidTestCompile 'com.android.support:support-annotations:23.2.1'

    //Google play services
    compile 'com.google.android.gms:play-services-auth:8.4.0'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'

    //Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    //Okhttp mockwebserver
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.2.0'


    //Recyclerview & Cardview
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'

    //Fuel (networking library)
    compile 'com.github.kittinunf.fuel:fuel-android:1.3.1'

    compile 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'

    //Kotlin promises
    compile 'nl.komponents.kovenant:kovenant-core:3.3.0'
    compile 'nl.komponents.kovenant:kovenant-android:3.3.0'

    //Kotlin anko
    compile 'org.jetbrains.anko:anko-sdk15:0.9'
    compile 'org.jetbrains.anko:anko-support-v4:0.9'
}

apply plugin: 'com.google.gms.google-services'
